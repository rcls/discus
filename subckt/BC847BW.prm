**********************************************************
*
* BC847BW
*
* Nexperia
*
* General purpose NPN transistor
* IC   = 100 mA
* VCEO = 45 V 
* hFE  = 200 - 450 @ 5V/2mA
* 
*
*
*
* Package pinning does not match Spice model pinning.
* Package: SOT 323
* 
* Package Pin 1: Base
* Package Pin 2: Emitter
* Package Pin 3: Collector
* 
*
* Extraction date (week/year): 45/2023
* Spicemodel includes temperature dependency
*
**********************************************************
*#
* Please note: Diode D1, Transistor Q2 and resistor RQ 
* are dedicated to improve modeling of quasi
* saturation area and reverse mode operation
* and do not reflect physical devices.
*
.SUBCKT BC847BW 1 2 3
Q1 1 2 3 MAIN 0.9605
Q2 11 2 3 MAIN 0.03951
RQ 11 1 505.8
D1 2 1 DIODE
*
.MODEL MAIN NPN
+ IS = 1.329E-14
+ NF = 1.005
+ ISE = 2.542E-15
+ NE = 1.353
+ BF = 272
+ IKF = 0.1503
+ VAF = 22.96
+ NR = 1.005
+ ISC = 1.49E-15
+ NC = 5.161
+ BR = 7.762
+ IKR = 0.009746
+ VAR = 25
+ RB = 420
+ IRB = 3.5E-05
+ RBM = 0.8
+ RE = 0.4391
+ RC = 0.5637
+ XTB = 1.227
+ EG = 1.11
+ XTI = 0.1241
+ CJE = 1.555E-11
+ VJE = 0.6394
+ MJE = 0.2591
+ TF = 6.486E-10
+ XTF = 25
+ VTF = 4.326
+ ITF = 0.3087
+ PTF = 0
+ CJC = 4.095E-12
+ VJC = 0.4807
+ MJC = 0.264
+ XCJC = 1
+ TR = 1.2E-06
+ CJS = 0
+ VJS = 0.75
+ MJS = 0.333
+ FC = 0.851
.MODEL DIODE D
+ IS = 3.184E-15
+ N = 1.026
+ BV = 1000
+ IBV = 0.001
+ RS = 3772
+ CJO = 0
+ VJ = 1
+ M = 0.5
+ FC = 0
+ TT = 0
+ EG = 1.11
+ XTI = 3
.ENDS
*